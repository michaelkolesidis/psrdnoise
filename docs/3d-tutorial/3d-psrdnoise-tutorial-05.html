<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
	"http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">

<html>
<head>
    <title>3-D PSRDnoise tutorial 05</title>
    <meta charset="utf-8">
	<link type="text/css" rel="stylesheet" href="3d-psrdnoise-tutorial.css">

<script type="text/javascript" src="syntaxhighlighter_3.0.83/scripts/shCore.js"></script>
<script type="text/javascript" src="syntaxhighlighter_3.0.83/scripts/shBrushGLSL.js"></script>
<link type="text/css" rel="stylesheet" href="syntaxhighlighter_3.0.83/styles/shCoreEclipse.css"/>

</style>

<script type="text/javascript">
function showshader() {
	codepane = document.getElementById("fragmentcode");
	codepane.innerHTML = document.getElementById("fs-visible").textContent;
	SyntaxHighlighter.highlight(codepane);
}
</script>

</head>
<body onload="showshader();">
<h2>3-D PSRDnoise tutorial</h2>
<h3>
	<a class="navigation" href="3d-psrdnoise-tutorial-01.html">Page 1</a>
	<a class="navigation" href="3d-psrdnoise-tutorial-02.html">2</a>
	<a class="navigation" href="3d-psrdnoise-tutorial-03.html">3</a>
	<a class="navigation" href="3d-psrdnoise-tutorial-04.html">4</a>
	<a class="navigation" href="3d-psrdnoise-tutorial-05.html">5</a>
	<a class="navigation" href="3d-psrdnoise-tutorial-06.html">6</a>
	<a class="navigation" href="3d-psrdnoise-tutorial-07.html">7</a>
</h3>
    <div class="grid-container">
		<div class="canvas" id="three-container">
		</div>
		<div class="code">
		<h1>5: Firing up</h1>
		<p>This is a less straightforward application of the "flow noise" technique. Instead of doing a sum of closely spaced octaves, we skip one octave after the first and take a jump with a factor 4 to create a more blotchy pattern on the largest scale with noticeable "hot spots", we don't warp the texture as much, and we warp each term progressively less. We then generate a pattern based partly on the noise value, but primarily on the length of the sum of the gradients for a more "wormy" look. Finally, we apply a fire-like color scheme with a blend from black through orange to yellow.</p>
		<p>This is a somewhat "brittle" shader: changing some of its parameters even a little can change the pattern quite a lot, and it took some tweaking to get the look we wanted. We were aiming for a sort of stylized, cartoonish rendition of the turbulent surface of a star. Whether we succeded is of course a matter of opinion. In any case, we think this is a good example to show the great flexibility of animated "flow noise". The method seems to have fallen into oblivion since it was first presented back in in 2001, and that's a shame.</p>
		<pre class="brush: GLSL;" id = "fragmentcode">
		// Fragment shader code goes here
		</pre>
		</div>
	</div>
</body>

<script type="x-shader/x-vertex" id="vs-visible">
uniform float time;

varying vec3 uvw;
varying vec2 st;
varying vec3 newnormal;

void main()
{
	uvw = position;
	st = uv;
	newnormal = (modelViewMatrix * vec4(normal,0.0)).xyz;
    gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
}
</script>

<script type="x-shader/x-fragment" id="fs-visible">
uniform float time;

varying vec3 uvw;

void main()
{
    vec3 v = 4.0*vec3(uvw);
	vec3 p = vec3(0.0);
	vec3 g, g1;
	float alpha = 0.25*time;

	float n = psrdnoise(v, p, alpha, g);
	
	float s = 4.0; // Skip scale 2.0
	float w = 0.5;
	for(float i=0.0; i<4.0; i++) {
		n += w * psrdnoise(s*v + w*0.05*g, p, s*alpha, g1);
		g += 2.0*w*g1;
		w *= 0.5;
		s *= 2.0;
	}

	float m = 0.1 + 0.1*n + 0.1*length(g);

	vec3 orange = vec3(1.0,0.5,0.0);
	vec3 mixcolor = 2.0 * m * orange; // clamps to yellow
    gl_FragColor = vec4(mixcolor, 1.0);
}
</script>

<script type="x-shader" id="includes-vs-hidden">
</script>

<script type="x-shader" id="includes-fs-hidden">
#ifdef GL_OES_standard_derivatives
#extension GL_OES_standard_derivatives : enable
#endif

#ifdef GL_ES
precision highp float;
#endif

// psrdnoise (c) Stefan Gustavson and Ian McEwan,
// ver. 2021-12-02, published under the MIT license:
// https://github.com/stegu/psrdnoise/

vec4 permute(vec4 i) {
	vec4 im = mod(i, 289.0);
	return mod(((im*34.0)+10.0)*im, 289.0);
}

float psrdnoise(vec3 x, vec3 period, float alpha, out vec3 gradient)
{
	const mat3 M = mat3(0.0, 1.0, 1.0, 1.0, 0.0, 1.0,  1.0, 1.0, 0.0);
	const mat3 Mi = mat3(-0.5, 0.5, 0.5, 0.5,-0.5, 0.5, 0.5, 0.5,-0.5);
	vec3 uvw = M * x;
	vec3 i0 = floor(uvw), f0 = fract(uvw);
	vec3 g_ = step(f0.xyx, f0.yzz), l_ = 1.0 - g_;
	vec3 g = vec3(l_.z, g_.xy), l = vec3(l_.xy, g_.z);
	vec3 o1 = min( g, l ), o2 = max( g, l );
	vec3 i1 = i0 + o1, i2 = i0 + o2, i3 = i0 + vec3(1.0);
	vec3 v0 = Mi * i0, v1 = Mi * i1, v2 = Mi * i2, v3 = Mi * i3;
	vec3 x0 = x - v0, x1 = x - v1, x2 = x - v2, x3 = x - v3;
	if(any(greaterThan(period, vec3(0.0)))) {
		vec4 vx = vec4(v0.x, v1.x, v2.x, v3.x);
		vec4 vy = vec4(v0.y, v1.y, v2.y, v3.y);
		vec4 vz = vec4(v0.z, v1.z, v2.z, v3.z);
		if(period.x > 0.0) vx = mod(vx, period.x);
		if(period.y > 0.0) vy = mod(vy, period.y);
		if(period.z > 0.0) vz = mod(vz, period.z);
		i0 = floor(M * vec3(vx.x, vy.x, vz.x) + 0.5);
		i1 = floor(M * vec3(vx.y, vy.y, vz.y) + 0.5);
		i2 = floor(M * vec3(vx.z, vy.z, vz.z) + 0.5);
		i3 = floor(M * vec3(vx.w, vy.w, vz.w) + 0.5);
	}
	vec4 hash = permute( permute( permute( 
			vec4(i0.z, i1.z, i2.z, i3.z ))
			+ vec4(i0.y, i1.y, i2.y, i3.y ))
			+ vec4(i0.x, i1.x, i2.x, i3.x ));
	vec4 theta = hash * 3.883222077;
	vec4 sz = hash * -0.006920415 + 0.996539792;
	vec4 psi = hash * 0.108705628;
	vec4 Ct = cos(theta), St = sin(theta);
	vec4 sz_prime = sqrt( 1.0 - sz*sz );
	vec4 gx, gy, gz;
	if(alpha != 0.0) {
		vec4 px = Ct * sz_prime, py = St * sz_prime, pz = sz;
		vec4 Sp = sin(psi), Cp = cos(psi), Ctp = St*Sp - Ct*Cp;
		vec4 qx = mix( Ctp*St, Sp, sz), qy = mix(-Ctp*Ct, Cp, sz);
		vec4 qz = -(py*Cp + px*Sp);
		vec4 Sa = vec4(sin(alpha)), Ca = vec4(cos(alpha));
		gx = Ca*px + Sa*qx; gy = Ca*py + Sa*qy; gz = Ca*pz + Sa*qz;
	}
	else {
		gx = Ct * sz_prime; gy = St * sz_prime; gz = sz;  
	}
	vec3 g0 = vec3(gx.x, gy.x, gz.x), g1 = vec3(gx.y, gy.y, gz.y);
	vec3 g2 = vec3(gx.z, gy.z, gz.z), g3 = vec3(gx.w, gy.w, gz.w);
	vec4 w = 0.5-vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3));
	w = max(w, 0.0); vec4 w2 = w * w, w3 = w2 * w;
	vec4 gdotx = vec4(dot(g0,x0), dot(g1,x1), dot(g2,x2), dot(g3,x3));
	float n = dot(w3, gdotx);
	vec4 dw = -6.0 * w2 * gdotx;
	vec3 dn0 = w3.x * g0 + dw.x * x0;
	vec3 dn1 = w3.y * g1 + dw.y * x1;
	vec3 dn2 = w3.z * g2 + dw.z * x2;
	vec3 dn3 = w3.w * g3 + dw.w * x3;
	gradient = 39.5 * (dn0 + dn1 + dn2 + dn3);
	return 39.5 * n;
}

</script>

<script src="js/three.min.js"></script>
<script src="js/OrbitControls.js"></script>
	
<script type="text/javascript" id="mainCode">
var
    container,
    renderer,
    scene,
    camera,
    mesh,
    start = Date.now(),
    fov = 45;

window.addEventListener( 'load', function() {
    container = document.getElementById( "three-container" );
    scene = new THREE.Scene();
    camera = new THREE.PerspectiveCamera(
        fov,
        1.0,
        0.1,
        100 );
    camera.position.z = 3.0;
    material = new THREE.ShaderMaterial( {
		uniforms: {
			time: {
				type: "f",
				value: 0.0
			}
		},
	vertexShader: document.getElementById('includes-vs-hidden').textContent
      + "\r\n" + document.getElementById( 'vs-visible' ).textContent,
    fragmentShader: document.getElementById('includes-fs-hidden').textContent
	  + "\r\n" + document.getElementById( 'fs-visible' ).textContent
	} );

	mesh = new THREE.Mesh(new THREE.SphereGeometry( 1.0, 100, 100 ), material);
	scene.add( mesh );

	renderer = new THREE.WebGLRenderer();
	renderer.setSize( 500, 500 );
	renderer.setPixelRatio( window.devicePixelRatio );
	renderer.getContext().getExtension("OES_standard_derivatives");

	const orbit = new THREE.OrbitControls( camera, renderer.domElement );
	container.appendChild( renderer.domElement );

	onWindowResize();
	window.addEventListener( 'resize', onWindowResize );

	render();
} );

function onWindowResize () {
	renderer.setSize( 500, 500 );
	camera.aspect = 1.0;
	camera.updateProjectionMatrix();
}

function render() {
    material.uniforms[ 'time' ].value = .001 * ( Date.now() - start );
    renderer.render( scene, camera );
    requestAnimationFrame( render );
}

</script>

</html>
