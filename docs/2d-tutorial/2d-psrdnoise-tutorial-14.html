<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
	"http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">

<html>
<head>
<title>2-D PSRDnoise tutorial 14</title>
<meta http-equiv="content-type" content="text/html; charset=utf-8">

<link type="text/css" rel="stylesheet" href="2d-psrdnoise-tutorial.css">

<script type="text/javascript" src="2d-drawquad.js"></script>

<script type="text/javascript" src="syntaxhighlighter_3.0.83/scripts/shCore.js"></script>
<script type="text/javascript" src="syntaxhighlighter_3.0.83/scripts/shBrushGLSL.js"></script>
<link type="text/css" rel="stylesheet" href="syntaxhighlighter_3.0.83/styles/shCoreEclipse.css"/>

<script type="text/javascript">
function showshader() {
	codepane = document.getElementById("fragmentcode");
	codepane.innerHTML = document.getElementById("shader-fs-visible").textContent;
	SyntaxHighlighter.highlight(codepane);
}
</script>

<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 position;
    attribute vec2 texcoord;

    varying vec2 st;

    void main(void) {
        gl_Position = vec4(position, 1.0);
        st = texcoord;
    }
</script>

<script id="shader-fs-hidden" type="x-shader/x-fragment">
#ifdef GL_ES
precision highp float;
#endif

#ifdef GL_OES_standard_derivatives
#extension GL_OES_standard_derivatives : enable
#endif


// psrdnoise (c) Stefan Gustavson and Ian McEwan,
// ver. 2021-12-02, published under the MIT license:
// https://github.com/stegu/psrdnoise/

float psrdnoise(vec2 x, vec2 period, float alpha, out vec2 gradient)
{
	vec2 uv = vec2(x.x+x.y*0.5, x.y);
	vec2 i0 = floor(uv), f0 = fract(uv);
	float cmp = step(f0.y, f0.x);
	vec2 o1 = vec2(cmp, 1.0-cmp);
	vec2 i1 = i0 + o1, i2 = i0 + 1.0;
	vec2 v0 = vec2(i0.x - i0.y*0.5, i0.y);
	vec2 v1 = vec2(v0.x + o1.x - o1.y*0.5, v0.y + o1.y);
	vec2 v2 = vec2(v0.x + 0.5, v0.y + 1.0);
	vec2 x0 = x - v0, x1 = x - v1, x2 = x - v2;
	vec3 iu, iv, xw, yw;
	if(any(greaterThan(period, vec2(0.0)))) {
		xw = vec3(v0.x, v1.x, v2.x);
		yw = vec3(v0.y, v1.y, v2.y);
		if(period.x > 0.0)
			xw = mod(vec3(v0.x, v1.x, v2.x), period.x);
		if(period.y > 0.0)
			yw = mod(vec3(v0.y, v1.y, v2.y), period.y);
		iu = floor(xw + 0.5*yw + 0.5); iv = floor(yw + 0.5);
	} else {
		iu = vec3(i0.x, i1.x, i2.x); iv = vec3(i0.y, i1.y, i2.y);
	}
	vec3 hash = mod(iu, 289.0);
	hash = mod((hash*51.0 + 2.0)*hash + iv, 289.0);
	hash = mod((hash*34.0 + 10.0)*hash, 289.0);
	vec3 psi = hash*0.07482 + alpha;
	vec3 gx = cos(psi); vec3 gy = sin(psi);
	vec2 g0 = vec2(gx.x, gy.x);
	vec2 g1 = vec2(gx.y, gy.y);
	vec2 g2 = vec2(gx.z, gy.z);
	vec3 w = 0.8 - vec3(dot(x0, x0), dot(x1, x1), dot(x2, x2));
	w = max(w, 0.0); vec3 w2 = w*w; vec3 w4 = w2*w2;
	vec3 gdotx = vec3(dot(g0, x0), dot(g1, x1), dot(g2, x2));
	float n = dot(w4, gdotx);
	vec3 w3 = w2*w; vec3 dw = -8.0*w3*gdotx;
	vec2 dn0 = w4.x*g0 + dw.x*x0;
	vec2 dn1 = w4.y*g1 + dw.y*x1;
	vec2 dn2 = w4.z*g2 + dw.z*x2;
	gradient = 10.9*(dn0 + dn1 + dn2);
	return 10.9*n;
}


float aastep(float threshold, float value) {
  float afwidth = 0.7 * length(vec2(dFdx(value), dFdy(value)));
//  float afwidth = fwidth(value);  // fwidth(v) is |dFdx(v)| + |dFdy(v)|
  return smoothstep(threshold-afwidth, threshold+afwidth, value);
}

float gridlines(vec2 v, vec2 p, float width) {
  float distx = 0.0;
  if(p.x > 0.0) {
    distx = abs(mod(v.x+0.5*p.x,p.x)-0.5*p.x); // unsigned distance to nearest x line
  }
  float disty = 0.0;
  if(p.y > 0.0) {
    disty = abs(mod(v.y+0.5*p.y,p.y)-0.5*p.y);
  }
  float gridx = 1.0 - aastep(width, distx);
  float gridy = 1.0 - aastep(width, disty);
  return max(gridx, gridy);
}

</script>

<script id="shader-fs-visible" type="x-shader/x-fragment">
varying vec2 st;
uniform float time;

void main(void) {

	const vec2 nscale = 4.0*vec2(1.0,6.0); // Waves
	const float tscale = 6.0; // Tiles
	vec2 v = nscale*(st-0.5)+vec2(time*0.2,time);
	const vec2 p = vec2(0.0, 0.0);
	float alpha = 4.0*time;
	vec2 g;
	
	float n = psrdnoise(v, p, alpha, g);
	float w = clamp(0.6-st.t + 0.01*n, 0.0, 1.0);
	w += 0.2*smoothstep(0.0, 0.1, w);
	float mask = aastep(0.01,w); // "This is water"
	vec2 vwarp = (st-0.5)*tscale + 0.05*w*g*vec2(1.0,2.0);

	float tiles = gridlines(vwarp, vec2(1.0,1.0), 0.05);
	vec3 tilecol = vec3(0.0,0.7,1.0);
	vec3 groutcol = vec3(0.3,0.3,0.8);
	vec4 watercol = vec4(1.0,1.0,1.0,0.3);
	vec3 mixcol = mix(tilecol, groutcol, tiles);
	mixcol = mix(mixcol, watercol.rgb, mask*watercol.a);

	gl_FragColor = vec4(mixcol, 1.0);
}</script>

</head>

<body onload="webGLStart(); showshader();">
<h2>2-D PSRDnoise tutorial</h2>
<h3>
	<a class="navigation" href="2d-psrdnoise-tutorial-01.html">Page 1</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-02.html">2</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-03.html">3</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-04.html">4</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-05.html">5</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-06.html">6</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-07.html">7</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-08.html">8</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-09.html">9</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-10.html">10</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-11.html">11</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-12.html">12</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-13.html">13</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-14.html">14</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-15.html">15</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-16.html">16</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-17.html">17</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-18.html">18</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-19.html">19</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-20.html">20</a>&nbsp;
	<a class="navigation" href="2d-psrdnoise-tutorial-21.html">21</a>
</h3>
 <div class="grid-container">
  <div class="canvas">
    <canvas id="psrdnoise-canvas" style="border: none;" width="500" height="500"></canvas>
	<p>
	<form onsubmit="return false;">Frame rate:
	<input type="text" disabled id="fpscounter" size="4"/> FPS
	</form>
	</p>
  </div>
  <div class="code">
    <h1>14: Gradients can push things</h1>
	<p>We can use the noise gradient to displace the texture coordinates of other patterns. This works differently from simply displacing with a vector built from noise <em>values</em>, because it performs the displacement in a manner very similar to bump mapping, and can create very cheap but plausible-looking imitations of refractions and reflections.</p>
	<p>This rather weird shader warps the regular grid texture on the "tiled surface" by adding the gradient of a noise pattern to its texture coordinates. The amount of warping is varied with the y coordinate, creating an illusion of refraction in the water of a swimming pool, with more "refraction" (texure coordinate distortion) to the pattern where the "water surface is farther from the tiled wall". Of course, the refraction isn't computed in any real sense -- we don't even compute the 3-D position of the water surface or the pool wall. This is "visually plausible", but there is absolutely no physics modelling involved, just some carefully scaled texture warping with noise, created for a specific visual effect. We could easily add plausibly faked specular highlights to the "water surface", but let's not go overboard with this particular example.</p>
	<p>Note that this is all done by a <em>single</em> evaluation of the noise function. Displacing the texture coordinates in a less nice-looking fashion with a vector of two uncorrelated noise values would require <em>more</em> work for a less satisfactory result.</p>
	<p>Of course, the gradient can be used for proper bump mapping to simulate reflections and refractions with a lot more accuracy, and we will get to that. However, this example shows the utility of simply using the gradient from one noise texture evaluation to push around the texture coordinates for a second texture evaluation. We will use this in the next example.</p>
	<pre class="brush: GLSL;" id="fragmentcode">
	// Text in this pane will be dynamically replaced.
	</pre>
  </div>
</div>
</body>

</html>
